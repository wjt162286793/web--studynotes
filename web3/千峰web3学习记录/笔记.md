### 本地搭建区块链 ganache

#### 可视化界面

https://archive.trufflesuite.com/ganache/

#### 命令行

```powershell
npm i ganache -g
```

```powershell
ganache
```

或者具体文件夹初始化

```powershell
npm i ganache
```

```powershell
npx ganache

```

保持账号不变就加上 -d

```js
npx ganache -d
```

效果如下

![1720762010942](https://i-blog.csdnimg.cn/direct/21facac5d98c4cceb49fa56f7cc1e29f.png)

#### 手动添加网络

点击metaMask手动添加网络

网络名称 :随便写

新的RPC URL: 127.0.0.1:8545  (这里就是刚才上面node日志中的最后一行,会给一个地址  RPC Listening)

链id: 1337 (日志中倒数第二个)

货币符号:ETH

区块链浏览器:  可写可不写

### 交易操作

转账需要gas燃料费(跑腿钱)

### web3.js

下载

```shell
npm i web3
```

以下的代码,在1.8.0版本中可以正常运行

```powershell
npm install web3@1.8.0 --save
```

文档学习地址: learnblockchain.cn

注意:计算数据,在去区块链中,需要用到BigNumber Library,不过web3会自动引用

为以太币价值很高的时候,一个浮点不对也会让用户造成损失

BigNumber Library计算20位浮点以上的数据时,也会有问题

#### 基础Api

##### 获取最近的区块 eth.getBlockNumber

```javascript
const Web3 = require('web3')
let web3 = new Web3('http://localhost:8545')
// console.log(web3,'web3')

web3.eth.getBlockNumber().then(res=>{
    console.log(res,'获取最近区块')
})

```

##### 获取链id eth.getChainId

```javascript
web3.eth.getChainId().then(res=>{
    console.log(res,'获取链id')
})
```

##### 获取余额 eth.getChainId

```javascript
//参数为账户的公钥
web3.eth.getBalance('0x407d73d8a49eeb85d32cf465507dd71d507100c1').then(res=>{
    //单位是wei
    console.log(res,'获取余额')
})
```

##### 交易回调 eth.sendTransaction

```javascript
// 使用回调
web3.eth.sendTransaction({
    from: '0xde0B295669a9FD93d5F28D9Ec85E40f4cb697BAe',
    data: code // deploying a contracrt
}, function(error, hash){
    console.log('交易完毕1')
});

// 使用 promise
web3.eth.sendTransaction({
    from: '0xde0B295669a9FD93d5F28D9Ec85E40f4cb697BAe',
    to: '0x11f4d0A3c12e86B4b5F39B213F7E19D048276DAe',
    value: '1000000000000000'
})
.then(function(receipt){
    console.log('交易完毕2')
});
```

事件绑定

```javascript
// 使用事件发生器
web3.eth.sendTransaction({
    from: '0xde0B295669a9FD93d5F28D9Ec85E40f4cb697BAe',
    to: '0x11f4d0A3c12e86B4b5F39B213F7E19D048276DAe',
    value: '1000000000000000'
})
.on('transactionHash', function(hash){
    console.log('交易完毕3')
})
.on('receipt', function(receipt){
    console.log('交易完毕4')
})
.on('confirmation', function(confirmationNumber, receipt){ console.log('交易完毕5') })
.on('error', console.error); // 如果是 out of gas 错误, 第二个参数为交易收据
```

##### 获取meteMask账号 eth.getAccounts

```javascript
web3.eth.getAccounts()
.then(res=>{
    console.log(res,'获取账号')
});

```

结果

![1720775193742](https://i-blog.csdnimg.cn/direct/b09935aaf05e480787de403e71697ac6.png)

##### 授权metaMask账号 eth.requestAccounts

```javascript
//授权
web3.eth.requestAccounts().then(res=>{
    console.log(res,'授权后获取的账号数组')
})
```

### truffle框架

是一种智能合约的编写框架

#### 下载

```powershell
npm i truffle -g
```

#### 在指定目录下构建脚手架

```powershell
npx truffle init
```

#### 目录结构

contracts:  存放智能合约文件 sol格式的

migrations: truffle使用migration system来控制合约的部署

test: 测试文件存放文字(javascript or solidity)

truffle-config.js:配置文件

#### 配置(configjs):

development:本地连接的开发环境地址

advanced:将智能合约部署到链上是需要消耗gas的,address属性如果没有具体配置,就默认使用当前连接账户的第一账号中gas进行消耗。

### Solidity语言基础

#### 协议声明

每个文件都最上方都写,声明了协议版本号

```solidity
// SPDX-License-Identifier: GPL-3.0 
// 源码遵循协议， MIT...
pragma solidity >=0.4.16 <0.9.0; //限定solidity编译器版本
```

#### 基础示例

文件名:StudentStorage.sol

```solidity
// SPDX-License-Identifier: GPL-3.0 
// 源码遵循协议， MIT...
pragma solidity >=0.4.16 <0.9.0; //限定solidity编译器版本

//contract类似于js中class,这里的命名必须要和文件名一致
contract StudentStorage{

    //创建 两个变量 username ,age,   状态变量,在链上占据空间
    uint public age;    //数字类型
    string public name;   //字符串类型

    // struct ,动态数组， 映射， string
    function setData(string memory _name,uint _age) public{
         //_name,_age存储在内存中,如果不用memory,都会在链上,会增加gas的消耗
        // string memory a; //局部变量
        name = _name;
        age = _age;
    }

    // function test(uint x ,uint y) public pure returns (uint){
    //     return x+y;
    // }

    // view(视图函数，只访问不修改 状态) , pure （纯函数， 不访问，也不修改）
    function getData() public view returns (string memory,uint) {
        return (name,age);
    }
}

```

#### 数据存储位置:

storage: 合约里的状态变量默认存在storage,存储在链上

memory:函数里的参数和临时变量一般都用memory,存储在内存中,不上链

calldata:和memory相似,存储在内存中,不上链。和memory不同点在于calldata变量不可以修改,一般用于函数的参数

#### 数据类型

基础类型:uint

复杂类型:struct ,动态数组， 映射， string

#### 函数标记

view和pure

标记为view的话,为视图函数,只访问不修改,(不写view会怎么样:花钱呗)

pure为纯函数,不访问也不修改

#### 作用域

state variable(状态变量):状态变量是数据存储在链上的变量，所有合约内函数都可以访问 ，gas消耗高。状态变量在合约内、函数外声明。可以在函数里更改状态变量的值

local variable(局部变量):局部变量是仅在函数执行过程中有效的变量，函数退出后，变量无效。局部变量的数据存储在内存里，不上链，gas低。局部变量在函数内声明

global variable(全局变量):全局变量是全局范围工作的变量，都是solidity预留关键字。他们可以在函数内不声明直接使用（类似于 msg.sender,block.number）

#### 作用域类型

##### 状态变量的三种作用域类型

Public(状态变量):可以在内部访问，也可以通过消息访问。对于公共状态变量，将生成一个自动getter函数。

Internal(内部状态变量):只能从当前合约或其派生合约内访问

Private(私有状态变量):只能从当前合约内部访问，派生合约内不能访问

##### 函数类型声明

函数可以指定为external,public,internal或者private

* external:外部函数是合约接口的一部分，这意味着可以从其他合约或通过事务调用它们。但是内部无法调用。
* public:外部调用和内部都可以调用
* internal:只能从当前合约或从当前合约派生的合约中访问，外部无法访问它们，由于它们没有通过合约的 ABI 向外部公开，所以它们可以接受内部类型的参数，比如映射或存储引用。
* private:私有函数类似于内部函数，但它们在派生合约中不可见。

#### 合约编译

在合约所在的目录下:

```powershell
truffle compile
```

可以看到日志中有没报错,如果编译成功,会在文件夹中生成build文件夹,其中有json,json中的abi就是和web3.js进行交互的属性

#### 部署脚本

在migrations中编写js文件,文件名以数字开头

部署文件名:1_deploy.js

```powershell
//artifacts.require为引入模块,类似于require,参数不用路径,只要sol文件位置正确进行

const Contacts = artifacts.require("StudentStorage.sol")
//必须导出一个方法
module.exports = function(deployer){
   deployer.deploy(Contacts)
}
```

执行部署命令:

```powershell
truffle migrate
```

查看日志:如果出现部署等信息,就说明部署成功了,可以通过metaMask中的货币数量消耗来验证是否成功

部署会直接调用编译

执行truffle migrate后,之前已经部署的文件不会再进行部署

如果之前文件有修改,那么可以执行

```powershell
truffle migrate --reset
```

#### 合约部署前测试

##### 控制台测试:

```powershell
truffle console
```

打开终端测试

```powershell
const obj = await StudentStorage.deployed()
```

会出现一个非常复杂的对象

```powershell
obj.address
```

打印地址

```powershell
obj.setData("wjt",10000)
```

设置变量

```powershell
obj.getData()
```

获取刚才修改的信息

**想访问到刚才name和age,需要将合约代码修改,变量设置为public,而且还要重新部署才可以访问的到**

##### 脚本测试:

两种方式:1.mocha测试 2.自己编写测试脚本

以下为自己测试脚本

创建scripts文件夹,test.js

```js
const Contacts = artifacts.require("StudentStorage.sol")

module.exports = async (callback)=>{
    //部署
      const studentStorage = await Contacts.deployed()

      await studentStorage.setData("wjt",100)
      let res = await studentStorage.getData()
      console.log(res,'刚才设置的值')

      console.log(await studentStorage.name())
      console.log(await studentStorage.age())

      //必须得写,完成函数
      callback()

```

执行脚本

```powershell
npx truffle exec ./scripts/test.js
```

结果如下

![img](https://i-blog.csdnimg.cn/direct/8af8b46fa4f546b4b13a8447fe38ef3c.png)

#### 创建对象列表

创建StudentLiStStorage.sol合约文件

```solidity
// SPDX-License-Identifier: GPL-3.0 
// 源码遵循协议， MIT...
pragma solidity >=0.4.16 <0.9.0; //限定solidity编译器版本
contract StudentListStorage{
    //结构体
    struct Student{
        uint id;
        string name;
        uint age;
        address account;
    }

    //动态数组
    Student[] public StudentList;

    function addList(string memory _name,uint _age) public returns (uint){
         uint count = StudentList.length;
         uint index = count + 1;
         StudentList.push(Student(index,_name,_age,msg.sender));
         return StudentList.length;
    }

    //view(视图函数,只访问不修改)
    function getList() public view returns (Student[] memory){
        Student[] memory list = StudentList;
        return list;
    }
}
```

执行部署 deploy2.js

```js
const Contacts = artifacts.require("StudentListStorage.sol")

module.exports = (deployer)=>{
   deployer.deploy(Contacts)
}
```

测试  test2.js

```js
const Contacts = artifacts.require("StudentListStorage.sol")
module.exports = async (callback)=>{
   const studentListStorage = await Contacts.deployed()

   await studentListStorage.addList('wjt',100)
   let res = await studentListStorage.getList()
   console.log(res,'列表结果')
   console.log(await studentListStorage.StudentList(1),'第一项')
   //结构类似于[[0:'1',1:'wjt',2:'100',3:'0x---111',id:'1',name:'wjt','age':'100',account:'0x---111']]
   callback()
}
```

结果如下

![](https://i-blog.csdnimg.cn/direct/2cb4c04400474941b2c1e49b8e0c9338.png)

### web3.js和智能合约的打通

```html

<!DOCTYPE html>
<html lang="en">

<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>Document</title>
    <script src="../../node_modules/web3/dist/web3.min.js"></script>

</head>

<body>
    <input type="text" id="myname">
    <input type="number" id="myage">
    <button id="add">add</button>

    <ul id="list">

    </ul>
    <script type="module">
        // var web3 = await new Web3("http://localhost:8545");
        // console.log(web3,'web3的值')
   
        // //先授权
        // let account = await web3.eth.getAccounts()
        var web3 = new Web3(Web3.givenProvider || "http://localhost:8545");
   
     //先授权
     let account = await web3.eth.requestAccounts()

     async function getList(){
      console.log(studentStorage,'结果')
            let res=  await studentStorage.methods.getList().call()
            console.log(res,'结果')
            list.innerHTML = res.map(item=>`
  
                <li>${item.id}--${item.name}---${item.age}</li>
            `).join("")
        }
        console.log(account,'account的值')

        //连接智能合约程序
        const studentStorage = await new  web3.eth.Contract([
    {
      "inputs": [
        {
          "internalType": "uint256",
          "name": "",
          "type": "uint256"
        }
      ],
      "name": "StudentList",
      "outputs": [
        {
          "internalType": "uint256",
          "name": "id",
          "type": "uint256"
        },
        {
          "internalType": "string",
          "name": "name",
          "type": "string"
        },
        {
          "internalType": "uint256",
          "name": "age",
          "type": "uint256"
        },
        {
          "internalType": "address",
          "name": "account",
          "type": "address"
        }
      ],
      "stateMutability": "view",
      "type": "function",
      "constant": true
    },
    {
      "inputs": [
        {
          "internalType": "string",
          "name": "_name",
          "type": "string"
        },
        {
          "internalType": "uint256",
          "name": "_age",
          "type": "uint256"
        }
      ],
      "name": "addList",
      "outputs": [
        {
          "internalType": "uint256",
          "name": "",
          "type": "uint256"
        }
      ],
      "stateMutability": "nonpayable",
      "type": "function"
    },
    {
      "inputs": [],
      "name": "getList",
      "outputs": [
        {
          "components": [
            {
              "internalType": "uint256",
              "name": "id",
              "type": "uint256"
            },
            {
              "internalType": "string",
              "name": "name",
              "type": "string"
            },
            {
              "internalType": "uint256",
              "name": "age",
              "type": "uint256"
            },
            {
              "internalType": "address",
              "name": "account",
              "type": "address"
            }
          ],
          "internalType": "struct StudentListStorage.Student[]",
          "name": "",
          "type": "tuple[]"
        }
      ],
      "stateMutability": "view",
      "type": "function",
      "constant": true
    }
  ]
        ,"0x0A980d82cb54522e60ea23f4c76aDDb5c9D3f9F3");

   

        add.onclick = async  function(){
            console.log(myname.value,myage.value)  
            console.log(studentStorage,'???')
            await studentStorage.methods.addList(myname.value,Number(myage.value)).send({
                from:'0x5568FAb270b31dE249fa5631cFc77404B491B123'
            })
            // //添加完后， 获取列表
            getList()
        }
        //首页进入页面， 获取列表
        getList()

    </script>
</body>

</html>
```

web3.eth.Contract方法中,接收两个参数,部署合约完毕后json文件中的abi和address字段

效果如下

![1722311376162](https://i-blog.csdnimg.cn/direct/dd117a7b149f4158ba8000d1617722fe.png)

### 加密货币(代币)

#### 概念

在以太坊中可以表示任何东西,例如:信誉积分,股份资产,美元人民币等法定货币,黄金等等

ERC-20就是一套基于以太坊网络的标准代币发型协议,有了ERC-20,开发者可以得以高效、可靠、低成本的创建自己的项目代币

地址: https://github.com/ethereum/EIPs/blob/master/EIPS/eip-20.md

#### 功能

* 将代币从一个账户转到另一个账户
* 获取账户当前代币余额
* 获取网络上可用代币的总供应量
* 批准一个账户中一定代币金额,由第三方账户使用

#### 基础和必备的货币信息设置

* name : 货币名称
* symbol : 货币符号
* decimals : 货币位数
* totalSupply : 货币发行量

#### 创建WjtToken.sol文件

```solidity
  // SPDX-License-Identifier: GPL-3.0 
  // 源码遵循协议， MIT...
  pragma solidity >=0.4.16 <0.9.0; //限定solidity编译器版本

  contract WjtToken{
      string public name = "WjtToken";   //货币名称
      string public symbol = "WJT";   //货币符号
      uint256 public decimals = 18;   //货币位数
      uint256 public totalSupply;   //货币发行量

      constructor(){
          totalSupply = 1000000 * (10 ** decimals);
      }
  }
```

#### 测试

部署合约

```powershell
truffle migrate
```

命令行测试

```powershell
truffle console
```

测试各项基础信息

![1722324779535](https://i-blog.csdnimg.cn/direct/b1055856cfb24c2f832da7d1238b4141.png)

#### 转账

##### 1.编写合约

WjtToken.sol

```solidity
// SPDX-License-Identifier: GPL-3.0 
// 源码遵循协议， MIT...
pragma solidity >=0.4.16 <0.9.0; //限定solidity编译器版本
import "openzeppelin-solidity/contracts/utils/math/SafeMath.sol";   //引入这个包,为了后面的精确计算
contract WjtToken{
    string public name = "WjtToken";   //货币名称
    string public symbol = "WJT";   //货币符号
    uint256 public decimals = 18;   //货币位数
    uint256 public totalSupply;   //货币发行量
  
    //solidity中没有object对象数据类型,就是用mapping,类似于es6的map
    mapping (address => uint256) public balanceOf;
  
    constructor(){
        totalSupply = 1000000 * (10 ** decimals);
        balanceOf[msg.sender] = totalSupply;
    }
  
    // 内置的转账事件
    event Transfer(address indexed _from , address indexed _to, uint256 _value);


    function transfer(address _to, uint256 _value) public returns (bool success){
        //require相当于一个if逻辑判定,如果require中的条件成立,就会继续往下走;如果不成立,就会直接报错,终止后续的执行
        require(_to != address(0));    //地址必须为有效地址
        _transfer(msg.sender,_to,_value);
        return true;
    }

    function _transfer(address _from,address _to,uint256 _value) internal{
        require(balanceOf[_from] >= _value);     //账户余额大于转账金额
        balanceOf[_from] = SafeMath.sub(balanceOf[_from],_value); //转账方开始扣款
        balanceOf[_to] = SafeMath.add(balanceOf[_to],_value);//收钱方余额增加

        //触发转账事件
        emit Transfer(_from, _to, _value);
    }
}

```

##### 2.进行部署

```solidity
truffle migrate --reset
```

##### 3.编写测试脚本

```js
const WjtToken = artifacts.require("WjtToken.sol");

//小单位转化为大单位,wei转ether
const fromWei = (bn)=>{
    return web3.utils.fromWei(bn,'ether');
}

const toWei = (number)=>{
 return web3.utils.toWei(number.toString(),"ether");
}


module.exports = async function(callback){
 const wjttoken = await WjtToken.deployed()

 let res1 = await wjttoken.balanceOf("0x6E6b0f4A076Ad0bB1b1d6a9dc4aABa0f8994eacc") 
 console.log('发款账号',fromWei(res1))

 await wjttoken.transfer("0x80717f30a387B3F1AED66de1f0B6De98DE951850",toWei(10000))
 let res2 = await wjttoken.balanceOf("0x6E6b0f4A076Ad0bB1b1d6a9dc4aABa0f8994eacc");
 console.log("发完款后的发款方",fromWei(res2))

 let res3 = await wjttoken.balanceOf("0x80717f30a387B3F1AED66de1f0B6De98DE951850");
 console.log("收款账号",fromWei(res3))
 callback()
}
```

##### 4.执行测试脚本

```js
truffle exec .\transfer_wjttoken.js
```

![1722402805361](https://i-blog.csdnimg.cn/direct/ca95a1e2b5ee47a3867b696f136a92b8.png)

##### 5.查看代币

(1)打开metaMask,点击添加资产
![1722407001728](https://i-blog.csdnimg.cn/direct/a0ad0e93fa1d4529a9687d229ced455f.png)
(2)进入添加代币表单
![1722407186305](https://i-blog.csdnimg.cn/direct/cf99f0b36745449cb38b8df839802c96.png)
(3)找到代币合约地址,也就是部署合约文件时,控制台给的地址
![1722407013441](https://i-blog.csdnimg.cn/direct/5a1f98842ba943c4ba4cd98825a5fdbc.png)
(4)添加代币
![1722407244414](https://i-blog.csdnimg.cn/direct/580e005059e74ba785d1a12a43a0abfc.png)
(5)在metaMask上查看代币
![1722407280834](https://i-blog.csdnimg.cn/direct/b177c5fb46994f88a5d9f68c5cde9324.png)
![1722407288245](https://i-blog.csdnimg.cn/direct/907aac1104494f47ad1d8ebd7d4606ea.png)

#### 委托转账

就好比你给一个机构或者系统授权,让它可以用你的钱进行转账。

比如还房贷,你的还贷银行卡就是被你授权的对象,你需要现在里面存钱,然后每个月银行都会在你这张卡上自动扣款。

##### 代码示例

这里进行一个demo示例,我开通酷狗会员并且授权自动续费,每年到了自动续100¥/年

```solidity
// SPDX-License-Identifier: GPL-3.0 
// 源码遵循协议， MIT...
pragma solidity >=0.4.16 <0.9.0; //限定solidity编译器版本
import "openzeppelin-solidity/contracts/utils/math/SafeMath.sol";   //引入这个包,为了后面的精确计算
contract WjtToken{
    string public name = "WjtToken";   //货币名称
    string public symbol = "WJT";   //货币符号
    uint256 public decimals = 18;   //货币位数
    uint256 public totalSupply;   //货币发行量
  
    //solidity中没有object对象数据类型,就是用mapping,类似于es6的map
    mapping (address => uint256) public balanceOf;   //余额
     //对象内有对象的数据格式,类似于{王惊涛:{酷狗:1000,腾讯视频:2000}}   王惊涛给微信支付平台授权了,可以给酷狗和腾讯视频续费,酷狗额度1000,腾视额度2000
    mapping (address => mapping(address=>uint256)) public allowance;   //在第三方的储蓄

    constructor(){
        totalSupply = 1000000 * (10 ** decimals);
        balanceOf[msg.sender] = totalSupply;
    }
  
    // 内置的转账事件  (付款方,收款方,货币额度)
    event Transfer(address indexed _from , address indexed _to, uint256 _value);
    // 内置的授权转账事件 (金主,被授权机构,货币额度)
    event Approval(address indexed _owner,address indexed _spender,uint256 _value);

    //对外暴露的转账事件
    function transfer(address _to, uint256 _value) public returns (bool success){
        //require相当于一个if逻辑判定,如果require中的条件成立,就会继续往下走;如果不成立,就会直接报错,终止后续的执行
        require(_to != address(0));    //地址必须为有效地址
        _transfer(msg.sender,_to,_value);
        return true;
    }

    function _transfer(address _from,address _to,uint256 _value) internal{
        require(balanceOf[_from] >= _value);     //账户余额大于转账金额
        balanceOf[_from] = SafeMath.sub(balanceOf[_from],_value); //转账方开始扣款
        balanceOf[_to] = SafeMath.add(balanceOf[_to],_value);//收钱方余额增加

        //触发转账事件
        emit Transfer(_from, _to, _value);
    }

    //对外暴露的委托交易功能 (第三方机构,数额)
    function approve(address _spender,uint256 _value) public returns (bool success){
        require(_spender!=address(0));

        //msg.sender //当前进行功能的用户(浏览器登录者)
        allowance[msg.sender][_spender] = _value;
        emit Approval(msg.sender, _spender, _value);
        return true;
    }



    //被授权的交易机构进行调用 (如自动续费,自动还款等功能需要第三方调用这种功能,使用金主的钱)
    function transferFrom(address _from,address _to,uint256 _value) public returns (bool success){
        //_from  金主
        //_to 收款方
        // msg.sender 第三方交易者  ,因为到时候执行这个函数的对象一定是第三方机构

        //第三方机构中金主对于某收款方的授权额度 = 之前的授权额度-这次扣费的额度  
        //例如:王惊涛2024年在酷狗自动续费1年,本来有400元,花了100元,还剩300        储蓄对象.王惊涛.酷狗.现在的额度 = 储蓄对象.王惊涛.酷狗.之前的额度(400) - 扣除额度(100)
        allowance[_from][msg.sender] = SafeMath.sub(allowance[_from][msg.sender],_value);
        //在进行真正的扣款交易,王惊涛给酷狗支付100元
        _transfer(_from, _to, _value);
        return true;
    }
}

```

### 交易所

#### 思路:

* 交易所得设置一个收费的账号:feeAccout     -- 每次交易所获得的利润归谁,就好比你开一家麻将馆,得有个获取盈利的收款二维码
* 交易费率:feePercent  --每笔交易完成时,你能得到的中介费,相当于别人打一局麻将,麻将馆挣10块钱
* 用户可以存款,可以存以太币和自定义的一些货币      --办会员,给会员充值,具体货币或者游戏币
* 用户可以取款,提取自己以太币和各种代币   --取钱
* 有流动池,可以进行订单的创建,填充,取消等操作  --类似于dnf的金币寄售
* 

#### 存款

##### 创建交易所合约

Exchange.sol

```solidity
// SPDX-License-Identifier: GPL-3.0 
// 源码遵循协议， MIT...
pragma solidity >=0.4.16 <0.9.0; //限定solidity编译器版本
import "openzeppelin-solidity/contracts/utils/math/SafeMath.sol";
import "./WjtToken.sol";
contract Exchange{
  address public feeAccount;  //交易所的收费账号,类似于商贩的收款二维码
  uint256 public feePercent;  //费率,交易过程中的平台中介费
  address constant ETHER = address(0);   //以太币的地址,这里其实可以不用写0,可以是任何字符表示
  mapping(address=>mapping(address=>uint256)) public tokens;   //相当于数据库,每一种货币,每个使用者的存款额度都在这里进行存储

  /** 
    模拟token的结构:
    {
    '以太币0xxxx0000':{
        '王惊涛0xxx0001':100000,
        '谢宝庆0xxx0002':100
    },
    '惊涛币0xxx1111':{
        '王惊涛0xxx0001':9999999,
        '李相赫0xxx0002':9999998
    }
  }
  */

  //传入收费地址和费率
  constructor(address _feeAccount,uint256 _feePercent){
    feeAccount = _feeAccount;
    feePercent = _feePercent;
  }

  //存款内置事件
  event Deposit(address token,address user,uint256 amount,uint256 balance);

  //存以太币
  function depositEther() payable public{

    //token账本.以太币系统.xx人(账户现余额) = token账本.以太币系统.xx人(账户原额度) + 存款额度
    tokens[ETHER][msg.sender] = SafeMath.add(tokens[ETHER][msg.sender],msg.value);
    //触发事件存款内置函数
    emit Deposit(ETHER, msg.sender, msg.value, tokens[ETHER][msg.sender]);
  }

  //存其他币
  function depositToken(address _token , uint256 _amount) public {
    //首先排除以太币
        require(_token != ETHER);
    //调用某个方法强行从你的账户往当前交易所账户转钱,这里的this是指合约的部署地址
        require(WjtToken(_token).transferFrom(msg.sender,address(this),_amount));
        tokens[_token][msg.sender] = SafeMath.add(tokens[_token][msg.sender],_amount);

        emit Deposit(_token,msg.sender,_amount,tokens[_token][msg.sender]);
  }
}
```

##### 创建部署文件

(这里用的还是3_deploy.js这个文件)

```js
const Contacts = artifacts.require("WjtToken.sol")
const Exchange = artifacts.require('Exchange.sol')
module.exports = async function(deployer){
 const list = await web3.eth.getAccounts()  //全部账号
 await deployer.deploy(Contacts)
 //这里我们可以写一个固定的号,也可以默认去用模拟账号的第0个,list[0]
 await deployer.deploy(Exchange,"0x6E6b0f4A076Ad0bB1b1d6a9dc4aABa0f8994eacc",10);
}
```

进行部署

```
npx truffle migrate --reset
```

##### 创建测试脚本

存以太币

```js
const WjtToken = artifacts.require("WjtToken.sol")
const Exchange = artifacts.require("Exchange.sol")

const ETHER_ADDRESS = '0x0000000000000000000000000000000000000000'  //这里是0的16进制, 0x后面40个0,对应我们在Exchange.sol文件中给ETHER设置为adress(0)

const fromWei = (bn)=>{
  return web3.utils.fromWei(bn,'ether');
}

const toWei= (number)=>{
   return web3.utils.toWei(number.toString(),"ether")
}


module.exports = async (callback) => {
    const wjttoken = await WjtToken.deployed()
    const exchange = await Exchange.deployed()

    //存10个以太币
    await exchange.depositEther({
        from:'0x6E6b0f4A076Ad0bB1b1d6a9dc4aABa0f8994eacc',   //自己钱包里任意的一个账号地址
        value:toWei(10)
    })
  
    let balance_ether = await exchange.tokens(ETHER_ADDRESS,'0x6E6b0f4A076Ad0bB1b1d6a9dc4aABa0f8994eacc')
    console.log(fromWei(balance_ether),'交易所中这个账号名下的以太币额度')
    callback()
}
```

##### 测试

###### 以太币存储

执行前账户

![1722929724547](https://i-blog.csdnimg.cn/direct/8dc38e9bea1749dc8b8788c0b8cda09e.png)

执行测试命令

```
truffle exec .\test_deposit.js
```

![1722929741777](https://i-blog.csdnimg.cn/direct/efb0401676f749fcb45adcaf0efa75a1.png)

执行完毕查看账户信息

![1722929749848](https://i-blog.csdnimg.cn/direct/b3e1108e298d420eb7a0f1723d57a058.png)

###### 代币存储

测试脚本新增以下代码

```js
   //存100000个WJT币
   //先授权才行
   await wjttoken.approve(exchange.address,toWei(100000),{from:'0x6E6b0f4A076Ad0bB1b1d6a9dc4aABa0f8994eacc'})
   //存款WJT
   await exchange.depositToken(wjttoken.address,toWei(100000),{from:'0x6E6b0f4A076Ad0bB1b1d6a9dc4aABa0f8994eacc'})

   //tokens中该地址的存款
   let balance_wjt = await exchange.tokens(wjttoken.address,'0x6E6b0f4A076Ad0bB1b1d6a9dc4aABa0f8994eacc')
   console.log(fromWei(balance_wjt),'交易所中这个账号名下的WJT币额度')
```

执行测试

![1722934376579](https://i-blog.csdnimg.cn/direct/8d06957e00974d6fb1e92a69a24ff57c.png)

![1722934383094](https://i-blog.csdnimg.cn/direct/7e17fbc468f14743b15ac9ac94b36872.png)

#### 取款

在交易所合约文件Exchange.sol中添加

```solidity
  //取款内置事件
  event WithDraw(address token,address user,uint256 amount,uint256 balance);

   //取以太币
   function withdrawEther(uint256 _amount) public{
    //交易所账户中的某账户的以太币余额必须大于等于当前的取款额度,通俗讲,你想取10万,你卡里得先有10万
    require(tokens[ETHER][msg.sender] >= _amount);

    //账户现余额 = 账户余额 - 取款余额 
    tokens[ETHER][msg.sender] = SafeMath.sub(tokens[ETHER][msg.sender],_amount);

    //内置方法   payable(账户).transfer(额度)  合约调用者发送多少以太币
    payable(msg.sender).transfer(_amount);
    //触发内置方法
    emit WithDraw(ETHER, msg.sender, _amount ,tokens[EHTER][msg.sender]);
   }

   //取代币
   function withdrawToken(address _token,uint256 _amount) public{
    require(_token!=ETHER);
    require(tokens[_token][msg.sender] >= _amount);

    tokens[_token][msg.sender] = SafeMath.sub(tokens[_token][msg.sender],_amount);
    //调用WjtToken合约中的转账方法,这个transfer是我们自定义的方法
    require(WjtToken(_token).transfer(msg.sender,_amount));
    emit WithDraw(_token,msg.sender,_amount,tokens[_token][msg.sender]);
   }
```

编译部署,完了进行测试,同上

编写测试脚本

```js
const WjtToken = artifacts.require("WjtToken.sol")
const Exchange = artifacts.require("Exchange.sol")

const ETHER_ADDRESS = '0x0000000000000000000000000000000000000000'  //这里是0的16进制, 0x后面40个0,对应我们在Exchange.sol文件中给ETHER设置为adress(0)

const fromWei = (bn)=>{
  return web3.utils.fromWei(bn,'ether');
}

const toWei= (number)=>{
   return web3.utils.toWei(number.toString(),"ether")
}

module.exports = async (callback) => {
    const wjttoken = await WjtToken.deployed()
    const exchange = await Exchange.deployed()

     //取款以太币
    await exchange.withdrawEther(toWei(5),{from:'0x6E6b0f4A076Ad0bB1b1d6a9dc4aABa0f8994eacc'}) 
    let balance_ether = await exchange.tokens(ETHER_ADDRESS,'0x6E6b0f4A076Ad0bB1b1d6a9dc4aABa0f8994eacc')
    console.log(fromWei(balance_ether),'交易所中这个账号名下的以太币额度')

    //取款WJT
   await exchange.withdrawToken(wjttoken.address,toWei(500),{from:'0x6E6b0f4A076Ad0bB1b1d6a9dc4aABa0f8994eacc'})
   let balance_wjt = await exchange.tokens(wjttoken.address,'0x6E6b0f4A076Ad0bB1b1d6a9dc4aABa0f8994eacc')
   console.log(fromWei(balance_wjt),'交易所中这个账号名下的WJT币额度')
    callback()

}
```

执行测试脚本之前,记得要先执行存款的脚本,只有先存够一定的额度,才能进行取款

![1723011570107](https://i-blog.csdnimg.cn/direct/88ece1ac799d4625a2680e9f8334e8df.png)

#### 流动池

##### 完整代码

```solidity
// SPDX-License-Identifier: GPL-3.0 
// 源码遵循协议， MIT...
pragma solidity >=0.4.16 <0.9.0; //限定solidity编译器版本
import "openzeppelin-solidity/contracts/utils/math/SafeMath.sol";
import "./WjtToken.sol";
contract Exchange{
  address public feeAccount;  //交易所的收费账号,类似于商贩的收款二维码
  uint256 public feePercent;  //费率,交易过程中的平台中介费
  address constant ETHER = address(0);   //以太币的地址,这里其实可以不用写0,可以是任何字符表示
  mapping(address=>mapping(address=>uint256)) public tokens;   //相当于数据库,每一种货币,每个使用者的存款额度都在这里进行存储

  /** 
    模拟token的结构:
    {
    '以太币0xxxx0000':{
        '王惊涛0xxx0001':100000,
        '谢宝庆0xxx0002':100
    },
    '惊涛币0xxx1111':{
        '王惊涛0xxx0001':9999999,
        '李相赫0xxx0002':9999998
    }
  }
  */



     /**?  订单列表数据格式
     {
     '订单1':{
       id:1,
       user:'0x1111',
       ...
         },
     '订单2':{
       id:2,
       user:'0x1112',
       ...
         }
     } 
     */
  


    //订单结构体
    struct _Order{
        uint256 id;   //订单id
        address user;   //订单发起者
        address tokenGet;   //WJT货币(代币)的发行地址
        uint256 amountGet;  //代币的数量

        address tokenGive;  //兑换的地址
        uint256 amountGive; //要兑换的数量
        uint256 timestamp;    //时间戳
    }


    mapping(uint256 => _Order) public orders;   //订单列表

    mapping(uint256 => bool) public orderCancel;   //取消订单
    mapping(uint256 => bool) public orderFill;   //填充订单
    uint256 public orderCount;   //订单数

    //创建订单事件
    event Order(
        uint256 id,
        address user,
        address tokenGet,
        uint256 amountGet,
        address tokenGive,
        uint256 amountGive,
        uint256 timestamp
    );
    //取消订单事件
    event Cancel(
        uint256 id,
        address user,
        address tokenGet,
        uint256 amountGet,
        address tokenGive,
        uint256 amountGive,
        uint256 timestamp
    );

    //填充订单事件
    event Trade(
        uint256 id,
        address user,
        address tokenGet,
        uint256 amountGet,
        address tokenGive,
        uint256 amountGive,
        uint256 timestamp
    );


    //查余额
    function balanceOf(address _token,address _user) public view returns(uint256){
        return tokens[_token][_user];
    }

    //创建订单
    function makeOrder(address _tokenGet,uint256 _amountGet, address _tokenGive,uint256 _amountGive) public{
        //tokens中某货币的某某账号持有者手中的货币量必须大于订单的交易数
        require(balanceOf(_tokenGive, msg.sender)>= _amountGive,unicode"创建订单时余额不足");
        //通过+1设置订单的id
        orderCount =  SafeMath.add(orderCount,1);
        //订单列中的订单1 =Order构造(...)
        orders[orderCount] = _Order(orderCount,msg.sender,_tokenGet,_amountGet,_tokenGive,_amountGive,block.timestamp);
        //触发发出订单事件
        emit Order(orderCount,msg.sender,_tokenGet,_amountGet,_tokenGive,_amountGive,block.timestamp);
    }
  
    //取消订单
    function cancelOrder(uint256 _id) public {
        //创建myorder变量,格式为_Order结构体,赋值为订单中的_id
       _Order memory myorder = orders[_id];
       //变量中id指向的值和_id相同
       require (myorder.id == _id);
       //orderCancel变量中这个_id指向的值为true
       orderCancel[_id] = true;

       emit Cancel(myorder.id, msg.sender, myorder.tokenGet, myorder.amountGet, myorder.tokenGive, myorder.amountGive, block.timestamp);
    }

    //填充订单
    /*
    模拟场景:
    张三创建了一个订单,10代币 == 1以太币
    李四认为这笔交易比较值,决定填充(接受)这个订单
    */
    function fillOrder(uint256 _id) public{
      //id为订单的编号,myorder为创建订单列表中,李四选择填充的那个订单
        _Order memory myorder = orders[_id];
        //再次确认,加强保险
        require(myorder.id == _id);
        //交易所挣的交易费(中介费) = (订单中代币数量*费率)/100
        uint256 feeAmout = SafeMath.div(SafeMath.mul(myorder.amountGet,feePercent),100);
        //交易所账本中[以太币][张三] >= 订单规定的以太币数量   张三只有100元,买不起超过100元的东西
        require(balanceOf(myorder.tokenGive, myorder.user) >= myorder.amountGive,unicode"创建订单的用户余额不足");
        //交易所账本中[代币][李四] >= 订单中规定的代币数量 + 消费
        require(balanceOf(myorder.tokenGet, msg.sender) >= SafeMath.add(myorder.amountGet,feeAmout),unicode"填充订单的用户的余额不足");

        //(...)括号内的值为具体的含义


        //交易所账本[交易货币的地址(代币)][填充者(李四)] = 交易所账本[代币][李四] - (这笔订单中WJT币的数量+付给交易所的中介费)
        //李四卡里的代币数 = 李四卡里代币数的 - 订单规定的代币数 - 李四给的交易费
        tokens[myorder.tokenGet][msg.sender] = SafeMath.sub(tokens[myorder.tokenGet][msg.sender],SafeMath.add(myorder.amountGet,feeAmout)); 

        //交易所账本[代币][交易所的收费账号(麻将馆老板的收款二维码)] = 交易所账本[代币][小费收款地址] + 小费数量
        //麻将馆老板的代币账号额度 = 麻将馆老板的代币账号额度 + 小费
        tokens[myorder.tokenGet][feeAccount] = SafeMath.add(tokens[myorder.tokenGet][feeAccount],feeAmout);

        //交易所账本[代币][订单发起者(张三)] = 交易所账本[代币][张三] + 这笔订单的代币数量
        //张三卡里的代币数 = 张三卡里的代币数 + 订单中的代币数
        tokens[myorder.tokenGet][myorder.user] = SafeMath.add(tokens[myorder.tokenGet][myorder.user],myorder.amountGet);

        //交易所账本[以太币][李四] = 交易所账本[以太币][李四] + 订单中以太币的数量
        //李四卡里的以太币 = 李四卡里的以太币 + 订单中的以太币
        tokens[myorder.tokenGive][msg.sender] = SafeMath.add(tokens[myorder.tokenGive][msg.sender],myorder.amountGive);

        //交易所账本[以太币][张三] = 交易所账本[以太币][张三] - 订单中的以太币
        tokens[myorder.tokenGive][myorder.user] = SafeMath.sub(tokens[myorder.tokenGive][myorder.user],myorder.amountGive);

        //填充订单中这个id的值为true
        orderFill[_id] = true;
        //触发填充事件
        emit Trade(myorder.id, myorder.user, myorder.tokenGet, myorder.amountGet, myorder.tokenGive, myorder.amountGive, block.timestamp);

    }

  //传入收费地址和费率
  constructor(address _feeAccount,uint256 _feePercent){
    feeAccount = _feeAccount;
    feePercent = _feePercent;
  }

  //存款内置事件
  event Deposit(address token,address user,uint256 amount,uint256 balance);

  //存以太币
  function depositEther() payable public{

    //token账本.以太币系统.xx人(账户现余额) = token账本.以太币系统.xx人(账户原额度) + 存款额度
    tokens[ETHER][msg.sender] = SafeMath.add(tokens[ETHER][msg.sender],msg.value);
    //触发事件存款内置函数
    emit Deposit(ETHER, msg.sender, msg.value, tokens[ETHER][msg.sender]);
  }

  //存其他币
  function depositToken(address _token , uint256 _amount) public {
    //首先排除以太币
        require(_token != ETHER);
    //调用某个方法强行从你的账户往当前交易所账户转钱,这里的this是指合约的部署地址
        require(WjtToken(_token).transferFrom(msg.sender,address(this),_amount));
        tokens[_token][msg.sender] = SafeMath.add(tokens[_token][msg.sender],_amount);

        emit Deposit(_token,msg.sender,_amount,tokens[_token][msg.sender]);
  }

  //取款内置事件
  event WithDraw(address token,address user,uint256 amount,uint256 balance);

   //取以太币
   function withdrawEther(uint256 _amount) public{
    //交易所账户中的某账户的以太币余额必须大于等于当前的取款额度,通俗讲,你想取10万,你卡里得先有10万
    require(tokens[ETHER][msg.sender] >= _amount);

    //账户现余额 = 账户余额 - 取款余额 
    tokens[ETHER][msg.sender] = SafeMath.sub(tokens[ETHER][msg.sender],_amount);

    //内置方法   payable(账户).transfer(额度)  合约调用者发送多少以太币
    payable(msg.sender).transfer(_amount);
    //触发内置方法
    emit WithDraw(ETHER, msg.sender, _amount ,tokens[ETHER][msg.sender]);
   }

   //取代币
   function withdrawToken(address _token,uint256 _amount) public{
    require(_token!=ETHER);
    require(tokens[_token][msg.sender] >= _amount);

    tokens[_token][msg.sender] = SafeMath.sub(tokens[_token][msg.sender],_amount);
    //调用WjtToken合约中的转账方法,这个transfer是我们自定义的方法
    require(WjtToken(_token).transfer(msg.sender,_amount));
    emit WithDraw(_token,msg.sender,_amount,tokens[_token][msg.sender]);
   }

}
```

### 构架整合

#### 测试账号固定化

每次通过ganache获取的10个测试账号都是随机的,如果能固定,就会顺很多,所以这里就需要改进一下

```powershell
ganache -d
```

这样每次获取的账号都是固定的了

#### 打包生成的build文件夹位置固定

truffle部署后生成的build文件夹,其实我们需要动态的引入这些json文件中的变量,所以需要将build放置在固定的位置,方便引用

在truffle-config.js文件中,加入以下配置代码

```powershell
"contracts_build_directory":"./src/build"
```

将原本的truffle代码放入前端脚手架工程代码中

如下所示

![1723629650253](https://i-blog.csdnimg.cn/direct/46885c75149546ed900987fa1b6ee36b.png)

#### package.json文件

在这个文件中原本的type为"module",使用的是ES6的导入导出方法,这里去掉,可以使用Commonjs导入导出方法,否则编译不通过

```json
{
  "name": "wjt-exchange",
  "private": true,
  "version": "0.0.0",
  "scripts": {
    "dev": "vite",
    "build": "vite build",
    "lint": "eslint . --ext js,jsx --report-unused-disable-directives --max-warnings 0",
    "preview": "vite preview"
  },
  "dependencies": {
    "openzeppelin-solidity": "^4.6.0",
    "react": "^18.2.0",
    "react-dom": "^18.2.0",
    "truffle": "^5.11.5",
    "web3": "^1.8.0"
  },
  "devDependencies": {
    "@types/react": "^18.2.56",
    "@types/react-dom": "^18.2.19",
    "@vitejs/plugin-react": "^4.2.1",
    "eslint": "^8.56.0",
    "eslint-plugin-react": "^7.33.2",
    "eslint-plugin-react-hooks": "^4.6.0",
    "eslint-plugin-react-refresh": "^0.4.5",
    "vite": "^5.1.4"
  }
}

```
1111